default(N,3)
default(LOOPS,0)

define(NIL,255)

byte next[N] = { myloop(x,0,decr(N), `NIL',`,') };
bool locked[N];
byte tail=NIL, in_critical;

byte active;
byte waiting[N] = {myloop(x,0,decr(N),`0',`,') };

define(P, `process P_$1 {
byte pred;
state NCS, p2, p3, p4, p5,p6, CS, p9, p13, p10;
init NCS;
trans
 ifelse(LOOPS, 1, `NCS -> NCS {effect active = $1; },',`')
 NCS -> p2 { effect next[$1] = NIL, active = $1, waiting[$1] = 1; },
 p2 -> p3 { effect pred = tail, tail = $1, active = $1;},
 p3 -> CS { guard pred == NIL; effect in_critical = in_critical+1, active = $1, waiting[$1] = 0;},
 p3 -> p4 { guard pred != NIL; effect active = $1;},
 p4 -> p5 { effect locked[$1] = 1, active = $1; },
 p5 -> p6 { effect next[pred] = $1, active = $1; },
 p6 -> CS { guard locked[$1] == 0; effect in_critical = in_critical+1, active = $1, waiting[$1] = 0;},

 CS -> p9 { guard next[$1] == NIL; effect in_critical = in_critical-1, active = $1;},
 CS -> p13 { guard next[$1] != NIL; effect in_critical = in_critical-1, active = $1;},
 p9 -> NCS { guard tail == $1; effect tail = NIL, active = $1; },
 p9 -> p10 { guard tail != $1; effect active = $1; },
 p10 -> p13 { guard next[$1] != NIL; effect active = $1; },
 p13 -> NCS { effect locked[next[$1]] = 0, active = $1; };
}
')

forloop(i,0,decr(N), `P(i)')

system async;